apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = project.name
group = "com.natpryce"
version = hasProperty("-version") ? property("-version") : "SNAPSHOT"


repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:java-hamcrest:2.0.0.0"
    testCompile "org.hamcrest:hamcrest-junit:2.0.0.0"
}


jar {
    manifest {
        attributes 'Implementation-Title': 'Make it Easy',
                'Implementation-Vendor': 'com.natpryce',
                'Implementation-Version': version
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar, sourcesJar, javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: publishUsername, password: publishPassword)
        }
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: publishUsername, password: publishPassword)
        }

        pom.project {
            name archivesBaseName
            packaging 'jar'
            description ''
            url 'https://github.com/npryce/make-it-easy'

            scm {
                connection 'https://github.com/npryce/make-it-easy.git'
                url 'https://github.com/npryce/make-it-easy'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                }
            }

            developers {
                developer {
                    id 'nat.pryce'
                    name 'Nat Pryce'
                }
            }
        }
    }
}
